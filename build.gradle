apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'Main'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    /*
    For logging, code to slf4j API only.
    At runtime, choose your implementation (logback is just an example).
     */
    implementation group: 'org.slf4j', name: 'slf4j-api', version: 'latest.release'
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: 'latest.release'

    /*
    Bridge any other logging frameworks (used by any dependencies) to slf4j.
    https://www.slf4j.org/legacy.html "SLF4J bound to logback-classic with
    redirection of commons-logging, log4j and java.util.logging to SLF4J"
     */
    /* Jakarta Commons Logging. Also need to exclude 'commons-logging'. */
    runtimeOnly group: 'org.slf4j', name: 'jcl-over-slf4j', version: 'latest.release'
    /* Apache Log4J. */
    runtimeOnly group: 'org.slf4j', name: 'log4j-over-slf4j', version: 'latest.release'
    /* java.util.logging (JUL). Also need to use SLF4JBridgeHandler. */
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: 'latest.release'

    /*
    Useful utility library Guava.
     */
    implementation group: 'com.google.guava', name: 'guava', version: 'latest.release'

    /*
    JUnit 5 unit testing framework
     */
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
}

repositories {
    jcenter()
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}
